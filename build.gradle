import java.text.SimpleDateFormat

buildscript {
	dependencies{
		classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.21.0")
	}
}

plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.2.71'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.2.71'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

group = 'com.jahnelgroup'
version = '1.0.0'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	// Zuul
	maven { url 'https://repo.spring.io/milestone' }
	// gradle-docker
	maven { url "https://plugins.gradle.org/m2/" }
}

ext {
	set('springCloudVersion', 'Greenwich.RELEASE')
	set('mainClassNameForDocker', 'com.jahnelgroup.adproxy.AppKt')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation 'org.springframework.security:spring-security-ldap'
	implementation 'org.springframework.security:spring-security-config'
	implementation 'org.springframework:spring-context-support'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:+'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

// Docker
// =========================
// unpacking spring fat jar is faster in a docker container
task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}

docker {
	name "${project.group}/${bootJar.baseName}:${project.version}"
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency", 'MAIN_CLASS': mainClassNameForDocker])
}
// automatically run a build before containerizing the app
docker.dependsOn build


// Spring Boot
//
springBoot {
	buildInfo {
		properties {
			additional = [
					"time": buildTime()
			]
		}
	}
}

static def buildTime() {
	final dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ssZ")
	dateFormat.timeZone = TimeZone.getTimeZone('GMT')
	dateFormat.format(new Date())
}